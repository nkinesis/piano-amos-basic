'REANIMATE PIANO 
'BY GABRIEL C. ULLMANN, YANN-GAËL GUÉHÉNEUC, 2024 
'INSPIRED BY: https://www.youtube.com/watch?v=iW-v7SpgS9Q

'Draw title and keys 
'******************* 
Screen Open 1,600,400,8,Hires
Curs Off 
Ink 2
Text 10,10,"ReAnimate(d) Piano"
Text 10,20,"By Gabriel C. Ullmann, 2024"
P_DRAWKEYS[-1]

'Wait for a key, draw the keyboard, play the sound 
'************************************************* 
Do 
   Wait Key 
   For I=0 To 10
      If Key State($10+I)
         P_DRAWKEYS[I]
         While Key State($10+I)
            Play 37+I,1
         Wend 
         P_DRAWKEYS[-1]
      End If 
   Next I
Loop 

Procedure P_DRAWKEYS[PRESSEDKEYNUMBER]
   'White key positions 
   '******************* 
   XW=70
   YW=70
   WW=50
   HW=100
   
   'Black key positions 
   '******************* 
   XB=XW+WW
   YB=70
   WB=30
   HB=60
   
   'Draw keys 
   '*********   
   For I=0 To 5
      'Draw white keys 
      '***************     
      'Set color, either white or green (pressed)  
      '******************************************
      If PRESSEDKEYNUMBER=2*I
         Ink 5
      Else 
         Ink 2
      End If 
      Bar XW+(I*(WB+WW)),YW To XW+((WB+WW)*I+WW),YW+HW
      
      'If last iteration, do not draw a black key  
      '******************************************  
      If I<>5
         'Draw black keys 
         '*************** 
         'Set color, either black or green (pressed)
         '******************************************  
         If PRESSEDKEYNUMBER=2*I+1
            Ink 5
         Else 
            Ink 0
         End If 
         Bar XB+((WB+WW)*I),YB To XB+((WB+WW)*I+WB+WW),YB+HB
      End If 
   Next I
End Proc
End 

